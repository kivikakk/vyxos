#!/usr/bin/env fish

if [ (count $argv) -lt 1 ]
    echo "usage: "(status -f)" verb [@HOST] [args ...]"
    exit 1
end

if string match -qr '\A@(?<host>.+)\z' -- "$argv[2]"
    # Assuming target is NixOS.
    set -e argv[2]
    set verb $argv[1]
    set -e argv[1]
    exec nixos-rebuild --target-host $host $verb --use-remote-sudo --flake git+file://(readlink -f (dirname (status -f)))\?submodules=1 $argv
end

set verb $argv[1]
set -e argv[1]

switch (uname)
    case Linux
        exec nixos-rebuild $verb --use-remote-sudo --flake git+file://(readlink -f (dirname (status -f)))\?submodules=1 $argv
    case Darwin
        exec darwin-rebuild $verb --flake git+file://(readlink -f (dirname (status -f)))\?submodules=1 $argv
    case '*'
        echo "unknown uname: "(uname)
        exit 1
end
